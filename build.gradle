buildscript {
    apply from: 'lib.gradle'
    apply from: 'versions.gradle'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath dependency_versions.kotlinGradle
        classpath dependency_versions.mavenPublishing
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    afterEvaluate {

        if (!project.hasProperty("android")) return

        android {

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()
            }

            sourceSets.forEach { sourceSet ->
                sourceSet.jniLibs.srcDirs = ['libs', "libs/${sourceSet.name}"]
                sourceSet.java.srcDirs += "src/${sourceSet.name}/kotlin"
                sourceSet.assets.srcDirs += "src/${sourceSet.name}/assets"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}